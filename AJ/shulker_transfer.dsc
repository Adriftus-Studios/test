
easy_shulker_transfer:
  type: world
  debug: false
  events:
    on player right clicks block:
    - stop if:<player.item_in_hand.material.name.ends_with[shulker_box].not>
    - stop if:<context.location.material.name.ends_with[chest].not>
    - determine passively cancelled
    - define inventory <context.location.inventory>
    - if <player.item_in_hand.script.name.equals[big_shulker_item].not.if_null[true]>:
      - define items <player.item_in_hand.inventory_contents>
      - if <[inventory].can_fit[<[items]>]>:
        - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<list[]>
        - inventory swap d:<context.location.inventory> o:<context.location.inventory.include[<[items]>]>
        - stop
      - else:
        - while <[items].is_empty.not>:
          - define i <[items].get[1]>
          - if <[inventory].can_fit[<[i]>]>:
            - define inventory <[inventory].include[<[i]>]>
            - define items <[items].remove[1]>
          - else:
            - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<[items]>
            - inventory swap d:<context.location.inventory> o:<[inventory]>
            - stop
    - else:
      - stop if:<player.item_in_hand.has_flag[big_shulker].not>
      - define items <player.item_in_hand.flag[big_shulker]>
      - if <[inventory].can_fit[<[items]>]>:
        - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<list[]>
        - inventory swap d:<context.location.inventory> o:<context.location.inventory.include[<[items]>]>
        - stop
      - else:
        - while <[items].is_empty.not>:
          - define i <[items].get[1]>
          - if <[inventory].can_fit[<[i]>]>:
            - define inventory <[inventory].include[<[i]>]>
            - define items <[items].remove[1]>
          - else:
            - inventory flag d:<player.inventory> slot:<player.held_item_slot> big_shulker:<[items]>
            - inventory swap d:<context.location.inventory> o:<[inventory]>
            - stop
    on player left clicks block:
    - stop if:<player.item_in_hand.material.name.ends_with[shulker_box].not>
    - stop if:<context.location.material.name.ends_with[chest].not>
    - determine passively cancelled
    - if <player.item_in_hand.script.name.equals[big_shulker_item].not.if_null[true]>:
      - define inventory <player.item_in_hand.inventory_contents>
      - define items <context.location.inventory.list_contents.filter[material.name.ends_with[shulker_box].not]>
      - if <[inventory].include[<[items]>].size> < 28:
        - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<[items]>
        - inventory clear d:<context.location.inventory>
        - stop
      - else:
        - define new_items <player.item_in_hand.inventory_contents>
        - while <[items].is_empty.not>:
          - define i <[items].get[1]>
          - if <[new_items].size> < 27:
            - define items <[items].remove[1]>
            - define new_items <[new_items].include[<[i]>]>
          - else:
            - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<[new_items]>
            - adjust <context.location.inventory> contents:<[items]>
            - stop
    - else:
      - stop if:<player.item_in_hand.has_flag[big_shulker].not>
      - define inventory <player.item_in_hand.flag[big_shulker]>
      - define items <context.location.inventory.list_contents.filter[material.name.ends_with[shulker_box].not]>
      - if <[inventory].include[<[items]>].size> < 55:
        - inventory adjust d:<player.inventory> slot:<player.held_item_slot> inventory_contents:<[items]>
        - inventory clear d:<context.location.inventory>
        - stop
      - else:
        - define new_items <player.item_in_hand.flag[big_shulker]>
        - while <[items].is_empty.not>:
          - define i <[items].get[1]>
          - if <[new_items].size> < 54:
            - define items <[items].remove[1]>
            - define new_items <[new_items].include[<[i]>]>
          - else:
            - inventory flag d:<player.inventory> slot:<player.held_item_slot> big_shulker:<[new_items]>
            - adjust <context.location.inventory> contents:<[items]>
            - stop
